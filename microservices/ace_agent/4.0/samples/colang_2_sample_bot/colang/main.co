# ----------------------------------
# Bot intents
# Note: To enable the LLM prompt generation extraction use only one single statement
# -----------------------------------
flow bot express greeting
  # meta: bot intent
  bot express "Hi there! How can I help you?"
    or bot express "Welcome! Ask away"
    or bot express "Hello!"

flow bot express goodbye
  # meta: bot intent
  bot express "Goodbye!"
    or bot express "Have a nice day"
    or bot express "See you!"

# ----------------------------------
# User intents
# Note: To enable the LLM prompt generation extraction use only one single statement
# -----------------------------------
flow user expressed greeting
  # meta: user intent
  user said "hi"
    or user said "hello"
    or user said "hey"

flow user expressed insult
  # meta: user intent
  user said "you are so dumb"
    or user said "stupid bot"
    or user said "i will kill you"

flow user expressed done with conversation
  # meta: user intent
  user said "that is all for today"
    or user said "I want to stop"
    or user said "end the conversation"
    or user said "stop everything"
    or user said "exit"

flow user asked date time question
  # meta: user intent
  user said "what day is it"
    or user said "what time is it"
    or user said "what's the time"
    or user said "is it already evening"
    or user said "what day is tomorrow"

flow user asked for repetition
  # meta: user intent
  user said r"(?i)(repeat|rephrase|say that again|not underst.*)"

# ----------------------------------
# Main story
# -----------------------------------

flow handling greetings
  user expressed greeting
  bot express greeting

flow handling date time questions
  user asked date time question
  $current_date_time = await GetCurrentDateTimeAction()
  $response_to_user = """Anser the previous user question based on the {{$current_date_time}}. Return a single string only."""
  bot say $response_to_user

flow handling user insults
  user expressed insult
  bot say "Please don't use abusive language"

flow providing llm response
  unhandled user intent
  generate then continue interaction

flow handling repetition questions
  global $last_bot_message
  user asked for repetition
  bot say $last_bot_message

flow main
  # meta: exclude from llm
  
  # Activate technical flow that generates user intents for any unhandled user utterance
  activate trigger user intent for unhandled user utterance

  activate track bot utterance state
  
  # Activating flows that guardrail the conversation (this is only a simplified example) 
  log "conversation start"
  activate providing llm response
  activate handling user insults
  activate handling greetings
  activate handling date time questions
  activate handling repetition questions
  
  log "chit chat start"

  # User wants to end conversation
  user expressed done with conversation
  bot express goodbye
  log "conversation is done"
